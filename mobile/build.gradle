apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "net.waynepiekarski.glider"
        minSdkVersion 10
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        ndk {
            moduleName "native-jni"
            ldLibs "log"
            stl "stlport_static"
        }
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled true
            shrinkResources true
        }
    }
    sourceSets {
        main {
            jni.srcDirs = ['../glider/jni']
            java.srcDirs = ['src/main/java', '../common/src/main/java']
        }
    }

    buildTypes.debug.jniDebuggable true

    productFlavors {
        x86 {
            ndk {
                abiFilter "x86"
            }

            // this is the flavor part of the version code.
            // It must be higher than the arm one for devices supporting
            // both, as x86 is preferred.
            versionCode = 3
        }
        arm {
            ndk {
                abiFilter "armeabi-v7a"
            }
            versionCode = 2
        }
        mips {
            ndk {
                abiFilter "mips"
            }
            versionCode = 1
        }
        fat {
            // fat binary, lowest version code to be
            // the last option
            versionCode = 0
        }
    }

    // make per-variant version code
    applicationVariants.all { variant ->
        // get the single flavor
        def flavorVersion = variant.productFlavors.get(0).versionCode

        // set the composite code, use the last digit of my timestamp for the flavor version
        variant.mergedFlavor.versionCode = (defaultConfig.versionCode / 10) * 10 + flavorVersion
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    wearApp project(':wear')
    compile 'com.android.support:appcompat-v7:19.+'
    compile 'com.google.android.gms:play-services:+'
}
